/*
* Copyright 2016 Luca Martino.
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copyFile of the License at
*
* http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/



apply plugin: 'com.android.application'

// extra project-wide variables that can be reused later
ext {
    supportLibraryVersion = '1.0.0' //used across dependencies
    grpcVersion = '1.11.0' //gRPC library used for communication
}

// Main Android configuration block
android {
    namespace 'nie.translator.rtranslator' //package namespace(used in code and manifest)
    compileSdkVersion 33  //SDK version used to compile the app(API 33 → Android 13)
    ndkVersion "25.2.9519653"  //NDK (Native Development Kit) used for C/C++ builds
    // App’s default build configuration
    defaultConfig {
        applicationId "nie.translator.rtranslator" //Unique ID for your app
        targetSdkVersion 33  //Highest Android version the app is tested and optimized for
        versionCode 24 //Internal version number(used for updates)
        versionName '2.1.4' //User-facing version name displayed in app info
        minSdkVersion 24 // Minimum Android version(API 24 → Android 7.0)required to run the app
        // Specifies configuration for native (C/C++) code builds using CMake
        externalNativeBuild {
            cmake {
                cppFlags '' // Optional compiler flags for C++ (empty here)
                abiFilters 'arm64-v8a'  // Build only for 64-bit ARM devices(to reduce APK size)
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
            debuggable true
        }
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
        resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
    }
    productFlavors {
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

dependencies {
    implementation 'com.github.kyleduo:SwitchButton:4f1e0aad0d' //toggle button used in app settings and UI
    implementation "com.google.android.material:material:1.9.0" //buttons, text fields, dialogs
    implementation "androidx.cardview:cardview:1.0.0" //cards with shadows and rounded corners
    implementation "androidx.recyclerview:recyclerview:1.0.0" //efficient list display – used in history, language lists
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3' //flexible and responsive UI layouts
    implementation "androidx.preference:preference:1.1.0-alpha02"  //managing app settings and preferences
    implementation "androidx.core:core-splashscreen:1.0.1" //splash screen during app launch
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0" //manage app data and states during lifecycle changes
    implementation 'androidx.work:work-runtime:2.7.1' //Handles background tasks like periodic data syncing
    implementation 'androidx.exifinterface:exifinterface:1.3.7' // Reads image metadata – orientation, timestamps
    implementation 'com.microsoft.onnxruntime:onnxruntime-android:1.19.0' //Runs AI models(like Whisper,NLLB)efficiently on Android devices
    implementation 'com.microsoft.onnxruntime:onnxruntime-extensions-android:0.12.4' //additional ONNX tools-faster inference & audio/text ops
    implementation 'com.google.mlkit:language-id:17.0.5' //Detects the input language automatically before translation
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '5.1' //secure token handling/encrypted communication (JWT support)
    def room_version = '2.1.0'
    implementation "androidx.room:room-runtime:$room_version" //Local database support – stores translation history and settings
    annotationProcessor "androidx.room:room-compiler:$room_version" // Compiles Room database code at build time
    implementation "androidx.room:room-rxjava2:$room_version" //Integrates Room with reactive programming
    implementation "androidx.room:room-guava:$room_version" //Provides async database operations with Guava support
    testImplementation "androidx.room:room-testing:$room_version" // Used for testing Room database operations
    testImplementation 'junit:junit:4.12' //Unit testing framework for validating app logic
    androidTestImplementation 'androidx.test:runner:1.2.0' //Runs Android-specific test cases on device
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0' //UI testing – simulates user actions like clicks and inputs
}
